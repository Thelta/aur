# Maintainer: Erik Zenker <erikzenker at posteo dot de>
pkgname=prometheus-cpp
pkgver=1.1.0
pkgrel=1
pkgdesc="Prometheus Client Library for Modern C++"
arch=('i686' 'x86_64')
url="https://github.com/jupp0r/prometheus-cpp.git"
license=('MIT')
depends=()
makedepends=('cmake' 'clang')

source=("${pkgname}::git+https://github.com/jupp0r/prometheus-cpp.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"

  git submodule init
  git submodule update

  mkdir -p build && cd build

  cmake .. -DCMAKE_BUILD_TYPE="Release" \
  		   -DBUILD_SHARED_LIBS=ON \
           -DCMAKE_INSTALL_PREFIX=/usr
}

build() {
  cd "${srcdir}/${pkgname}/build"
  cmake --build .
}

check() {
  cd "${srcdir}/${pkgname}/build"
  ctest -VV
}

package() {
  cd "${srcdir}/${pkgname}/build"
  make DESTDIR="$pkgdir/" install
}
