pkgname=aws-c-event-stream-custom
_pkgname=aws-c-event-stream
conflicts=(aws-c-event-stream)
pkgver=0.1.4
pkgrel=1
pkgdesc='C99 implementation of the vnd.amazon.eventstream content-type'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-event-stream'
license=(Apache)
depends=(aws-c-common-custom aws-checksums-custom)
makedepends=(cmake)
source=(aws-c-event-stream-$pkgver.zip::https://github.com/awslabs/aws-c-event-stream/archive/v$pkgver.zip)
sha256sums=('8d64b99a4d8d28fa53fe38bf9600a1044e6ee62af6da5c729cec9741c259bf31')

build() {
  cd aws-c-event-stream-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -S . -B build
  cmake --build build
}

check() {
  cd aws-c-event-stream-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-event-stream-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
