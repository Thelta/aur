pkgname=aws-sdk-cpp-custom
_pkgname=aws-sdk-cpp
pkgver=1.7.347
conflicts=(aws-sdk-cpp aws-sdk-cpp-git)
pkgrel=1
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib libutil-linux aws-c-common-custom aws-c-event-stream-custom libpulse)
makedepends=(cmake)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip)
sha256sums=('d1beacdb5f01b82b6353a2960c5eee24337eaf23970c8cc647c664f7cf324355')

# AWS C++ SDK is a huge project. Install size of the whole project is ~500MiB. To reduce it
# we build only a small subset of components used by other packages. Add more components to
# this list if you need it.
_components="core;ec2;kinesis;firehose;sts;dynamodb"
# TOTHINK: it would be interesting to explore an idea of breaking sdk into set of small
# per-component packages "aws-sdk-cpp-ec2, aws-sdk-cpp-kinesis, ...".

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF -DSTATIC_LINKING=1 -DBUILD_ONLY="$_components" -S . -B build
  cmake --build build
}

package() {
  cd aws-sdk-cpp-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
