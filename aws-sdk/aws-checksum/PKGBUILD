
_pkgname=aws-checksums
pkgname=aws-checksums-custom
conflicts=(aws-checksums)
pkgver=0.1.5
pkgrel=1
pkgdesc='Cross-Platform HW accelerated CRC32c and CRC32 with fallback to efficient SW implementations.'
arch=(x86_64)
url='https://github.com/awslabs/aws-checksums'
license=(Apache)
makedepends=(cmake)
source=(aws-checksums-$pkgver.zip::https://github.com/awslabs/aws-checksums/archive/v$pkgver.zip)
sha256sums=('538aee3dd5fd58e0f6ec70f846c35263fcd9244fb62ede49fe8a700e489cd462')

build() {
  cd aws-checksums-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -S . -B build
  cmake --build build
}

check() {
  cd aws-checksums-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-checksums-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
