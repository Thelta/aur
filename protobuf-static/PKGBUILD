# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_pkgname=protobuf
pkgname=('protobuf-static')
pkgver=3.19.4
pkgrel=2
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('x86_64')
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=('gcc-libs' 'glibc' 'zlib')
makedepends=('unzip' 'python-setuptools')
source=("https://github.com/protocolbuffers/$_pkgname/releases/download/v$pkgver/$_pkgname-all-$pkgver.tar.gz")
sha512sums=('0d9f4ad25b8d932ff6c19c6db0fa452eb028bcc7ea428db0868c01e7e521f75f6d9516f7688166a95e3241d8c5209bc012b3a221276aab9f11f2afb743beb9ae')

prepare() {
  cd "$_pkgname-$pkgver"
  autoreconf -vfi

# Don't fail tests on deprecation warnings
  sed -e "/DeprecationWarning/d" -i python/google/protobuf/internal/*test.py
}

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr
  make -j12 CFLAGS="$CFLAGS -fPIC" CXXFLAGS="$CXXFLAGS -fPIC"
}

package_protobuf-static() {
  replaces=('protobuf-cpp')
  provides=('libprotobuf.a' 'libprotobuf-lite.a')

  cd "$_pkgname-$pkgver"
  pushd "src/.libs"
  install -vDm 644 libprotobuf.a -t "$pkgdir"/usr/lib/
  install -vDm 644 libprotobuf-lite.a -t "$pkgdir"/usr/lib/
  popd

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
